' Neljadimensionaalse risttahuka po"o"rlemine
' neljadimensionaalses ruumis. Iga risttahuka
' serv ka"sitsi lahti kirjutatud, mis vo~imaldab
' kasutada maksimaalse hulga va"rve servade
' kuvamisel. (Programm optimeerimata.)

DEFINT I-Q
DEFSNG A-H
DEFSNG R-Z

DIM DF(1 TO 4, 1 TO 4)
DIM F(1 TO 4, 1 TO 4)
DIM R(1 TO 4, 1 TO 4)
DIM RI(1 TO 4, 1 TO 4)

DIM TIP4(1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2, 1 TO 4)
DIM TIP4N(1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2, 1 TO 4)
DIM TIP3(1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2, 1 TO 3)
DIM TIP2(1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2)
DIM T(1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2, 1 TO 2) AS INTEGER

SCREEN 9
PALETTE 1, 15
PALETTE 2, 18
PALETTE 3, 22
PALETTE 4, 27
PALETTE 5, 36
PALETTE 6, 37
PALETTE 7, 38
PALETTE 8, 39
PALETTE 9, 46
PALETTE 10, 47
PALETTE 11, 50
PALETTE 12, 52
PALETTE 13, 54
PALETTE 14, 62

TWOPI = 6.2832

FISTEREO = .15

SCALEX = 80
SCALEY = 70
NXPIX = 640
NYPIX = 350

A = .99
B = .61
C = .86
D = .74

DFXY = .0055
DFXZ = .0027
DFYZ = .0034
DFXW = .0002
DFYW = .0039
DFZW = .0091
FAC = 5!

DFXY = DFXY * FAC
DFXZ = DFXZ * FAC
DFYZ = DFYZ * FAC
DFXW = DFXW * FAC
DFYW = DFYW * FAC
DFZW = DFZW * FAC

FOR P = 1 TO 4
FOR Q = 1 TO 4
IF P = Q THEN RI(P, Q) = 1! ELSE RI(P, Q) = 0!
NEXT Q
NEXT P

FOR M = 1 TO 4
FOR N = 1 TO 4
F(M, N) = 0!
NEXT N
NEXT M

DF(2, 1) = DFXY
DF(3, 1) = DFXZ
DF(3, 2) = DFYZ
DF(4, 1) = DFXW
DF(4, 2) = DFYW
DF(4, 3) = DFZW

APage% = 1
VPage% = 0

DO
	SCREEN 9, , APage%, VPage%
	CLS 1

FOR I = 1 TO 2
FOR J = 1 TO 2
FOR K = 1 TO 2
FOR L = 1 TO 2
	TIP4(1, J, K, L, 1) = A
	TIP4(2, J, K, L, 1) = -A
	TIP4(I, 1, K, L, 2) = B
	TIP4(I, 2, K, L, 2) = -B
	TIP4(I, J, 1, L, 3) = C
	TIP4(I, J, 2, L, 3) = -C
	TIP4(I, J, K, 1, 4) = D
	TIP4(I, J, K, 2, 4) = -D
NEXT L
NEXT K
NEXT J
NEXT I

FOR M = 2 TO 4
	N1 = M - 1
	FOR N = 1 TO N1

		FI = F(M, N)
		FI = FI + DF(M, N)
		IF FI > TWOPI THEN FI = FI - TWOPI
		IF FI < 0! THEN FI = FI + TWOPI
		F(M, N) = FI

		SINUS = SIN(FI)
		COSINUS = COS(FI)

		FOR P = 1 TO 4
		FOR Q = 1 TO 4
		R(P, Q) = RI(P, Q)
		NEXT Q
		NEXT P

		R(M, M) = COSINUS
		R(N, N) = COSINUS
		R(M, N) = SINUS
		R(N, M) = -SINUS

		FOR I = 1 TO 2
		FOR J = 1 TO 2
		FOR K = 1 TO 2
		FOR L = 1 TO 2
		FOR P = 1 TO 4
			TIP = 0!

				FOR Q = 1 TO 4
				TIP = TIP + R(P, Q) * TIP4(I, J, K, L, Q)
				NEXT Q

			TIP4N(I, J, K, L, P) = TIP
		NEXT P
		NEXT L
		NEXT K
		NEXT J
		NEXT I

		FOR I = 1 TO 2
		FOR J = 1 TO 2
		FOR K = 1 TO 2
		FOR L = 1 TO 2
		FOR P = 1 TO 4
			TIP4(I, J, K, L, P) = TIP4N(I, J, K, L, P)
		NEXT P
		NEXT L
		NEXT K
		NEXT J
		NEXT I
	NEXT N
NEXT M

FOR I = 1 TO 2
FOR J = 1 TO 2
FOR K = 1 TO 2
FOR L = 1 TO 2
FOR P = 1 TO 3
	TIP3(I, J, K, L, P) = TIP4(I, J, K, L, P)
NEXT P
NEXT L
NEXT K
NEXT J
NEXT I

FOR ISTEREO = 1 TO 2

	IF ISTEREO = 1 THEN NSHIFTX = NXPIX / 4
	IF ISTEREO = 2 THEN NSHIFTX = NXPIX / 4 * 3
	NSHIFTY = NYPIX / 2

	IF ISTEREO = 1 THEN FI = .5 * FISTEREO
	IF ISTEREO = 2 THEN FI = TWOPI - .5 * FISTEREO

	SINUS = SIN(FI)
	COSINUS = COS(FI)

FOR I = 1 TO 2
FOR J = 1 TO 2
FOR K = 1 TO 2
FOR L = 1 TO 2
TIP2(I, J, K, L, 1) = TIP3(I, J, K, L, 1) * COSINUS - TIP3(I, J, K, L, 3) * SINUS
TIP2(I, J, K, L, 2) = TIP3(I, J, K, L, 2)
NEXT L
NEXT K
NEXT J
NEXT I

FOR I = 1 TO 2
FOR J = 1 TO 2
FOR K = 1 TO 2
FOR L = 1 TO 2
	T(I, J, K, L, 1) = TIP2(I, J, K, L, 1) * SCALEX + NSHIFTX
	T(I, J, K, L, 2) = TIP2(I, J, K, L, 2) * SCALEY + NSHIFTY
NEXT L
NEXT K
NEXT J
NEXT I

XPPPP = T(1, 1, 1, 1, 1)
YPPPP = T(1, 1, 1, 1, 2)
XMPPP = T(2, 1, 1, 1, 1)
YMPPP = T(2, 1, 1, 1, 2)
XPMPP = T(1, 2, 1, 1, 1)
YPMPP = T(1, 2, 1, 1, 2)
XMMPP = T(2, 2, 1, 1, 1)
YMMPP = T(2, 2, 1, 1, 2)
XPPMP = T(1, 1, 2, 1, 1)
YPPMP = T(1, 1, 2, 1, 2)
XMPMP = T(2, 1, 2, 1, 1)
YMPMP = T(2, 1, 2, 1, 2)
XPMMP = T(1, 2, 2, 1, 1)
YPMMP = T(1, 2, 2, 1, 2)
XMMMP = T(2, 2, 2, 1, 1)
YMMMP = T(2, 2, 2, 1, 2)
XPPPM = T(1, 1, 1, 2, 1)
YPPPM = T(1, 1, 1, 2, 2)
XMPPM = T(2, 1, 1, 2, 1)
YMPPM = T(2, 1, 1, 2, 2)
XPMPM = T(1, 2, 1, 2, 1)
YPMPM = T(1, 2, 1, 2, 2)
XMMPM = T(2, 2, 1, 2, 1)
YMMPM = T(2, 2, 1, 2, 2)
XPPMM = T(1, 1, 2, 2, 1)
YPPMM = T(1, 1, 2, 2, 2)
XMPMM = T(2, 1, 2, 2, 1)
YMPMM = T(2, 1, 2, 2, 2)
XPMMM = T(1, 2, 2, 2, 1)
YPMMM = T(1, 2, 2, 2, 2)
XMMMM = T(2, 2, 2, 2, 1)
YMMMM = T(2, 2, 2, 2, 2)

1 LINE (XPPPP, YPPPP)-(XMPPP, YMPPP), 1
2 LINE (XPMPP, YPMPP)-(XMMPP, YMMPP), 2
3 LINE (XPPMP, YPPMP)-(XMPMP, YMPMP), 3
4 LINE (XPMMP, YPMMP)-(XMMMP, YMMMP), 4
5 LINE (XPPPM, YPPPM)-(XMPPM, YMPPM), 4
6 LINE (XPMPM, YPMPM)-(XMMPM, YMMPM), 3
7 LINE (XPPMM, YPPMM)-(XMPMM, YMPMM), 2
8 LINE (XPMMM, YPMMM)-(XMMMM, YMMMM), 1

9 LINE (XPPPP, YPPPP)-(XPMPP, YPMPP), 5
10 LINE (XMPPP, YMPPP)-(XMMPP, YMMPP), 6
11 LINE (XPPMP, YPPMP)-(XPMMP, YPMMP), 7
12 LINE (XMPMP, YMPMP)-(XMMMP, YMMMP), 7
13 LINE (XPPPM, YPPPM)-(XPMPM, YPMPM), 7
14 LINE (XMPPM, YMPPM)-(XMMPM, YMMPM), 7
15 LINE (XPPMM, YPPMM)-(XPMMM, YPMMM), 6
16 LINE (XMPMM, YMPMM)-(XMMMM, YMMMM), 5

17 LINE (XPPPP, YPPPP)-(XPPMP, YPPMP), 8
18 LINE (XMPPP, YMPPP)-(XMPMP, YMPMP), 9
19 LINE (XPMPP, YPMPP)-(XPMMP, YPMMP), 10
20 LINE (XMMPP, YMMPP)-(XMMMP, YMMMP), 11
21 LINE (XPPPM, YPPPM)-(XPPMM, YPPMM), 11
22 LINE (XMPPM, YMPPM)-(XMPMM, YMPMM), 10
23 LINE (XPMPM, YPMPM)-(XPMMM, YPMMM), 9
24 LINE (XMMPM, YMMPM)-(XMMMM, YMMMM), 8

25 LINE (XPPPP, YPPPP)-(XPPPM, YPPPM), 12
26 LINE (XMPPP, YMPPP)-(XMPPM, YMPPM), 13
27 LINE (XPMPP, YPMPP)-(XPMPM, YPMPM), 14
28 LINE (XMMPP, YMMPP)-(XMMPM, YMMPM)
29 LINE (XPPMP, YPPMP)-(XPPMM, YPPMM)
30 LINE (XMPMP, YMPMP)-(XMPMM, YMPMM), 14
31 LINE (XPMMP, YPMMP)-(XPMMM, YPMMM), 13
32 LINE (XMMMP, YMMMP)-(XMMMM, YMMMM), 12

NEXT ISTEREO

SWAP APage%, VPage%

LOOP WHILE INKEY$ = ""

END